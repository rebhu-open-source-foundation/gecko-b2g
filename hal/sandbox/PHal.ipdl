/* -*- Mode: C++; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 2 -*- */
/* vim: set sw=2 ts=8 et ft=cpp : */
/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PContent;
include protocol PBrowser;

include "mozilla/dom/ReferrerInfoUtils.h";
include "mozilla/GfxMessageUtils.h";

using hal::ScreenOrientation from "mozilla/HalScreenConfiguration.h";
using mozilla::dom::BatteryHealth from "mozilla/dom/BatteryManagerBinding.h";
using mozilla::hal::SensorType from "mozilla/HalSensor.h";
using mozilla::hal::WakeLockControl from "mozilla/HalTypes.h";
using mozilla::hal::SwitchState from "mozilla/HalTypes.h";
using mozilla::hal::SwitchDevice from "mozilla/HalTypes.h";
using mozilla::hal::ProcessPriority from "mozilla/HalTypes.h";
using nsIntRect from "nsRect.h";
using PRTime from "prtime.h";
using mozilla::hal::FMRadioCountry from "mozilla/HalTypes.h";
using mozilla::hal::FMRadioOperation from "mozilla/HalTypes.h";
using mozilla::hal::FMRadioOperationStatus from "mozilla/HalTypes.h";

namespace mozilla {

namespace hal {
struct BatteryInformation {
  double level;
  bool   charging;
  double remainingTime;
  double temperature;
  BatteryHealth health;
  bool   present;
};

struct UsbStatus {
  bool deviceAttached;
  bool deviceConfigured;
};

struct PowerSupplyStatus {
  bool powerSupplyOnline;
  nsCString powerSupplyType;
};

struct SensorData {
  SensorType sensor;
  PRTime timestamp;
  float[] values;
};

struct NetworkInformation {
  uint32_t type;
  bool   isWifi;
  uint32_t dhcpGateway;
};

struct SwitchEvent {
  SwitchDevice device;
  SwitchState status;
};

struct WakeLockInformation {
  nsString topic;
  uint32_t numLocks;
  uint32_t numHidden;
  uint64_t[] lockingProcesses;
};

struct ScreenConfiguration {
  nsIntRect rect;
  ScreenOrientation orientation;
  uint16_t angle;
  uint32_t colorDepth;
  uint32_t pixelDepth;
};

struct FlashlightInformation {
  bool enabled;
  bool present;
};

struct FMRadioOperationInformation {
  FMRadioOperation operation;
  FMRadioOperationStatus status;
  uint32_t frequency;
};

struct FMRadioRDSGroup {
  uint16_t blockA;
  uint16_t blockB;
  uint16_t blockC;
  uint16_t blockD;
};

struct FMRadioSettings {
  FMRadioCountry country;
  uint32_t upperLimit;
  uint32_t lowerLimit;
  uint32_t spaceType;
  uint32_t preEmphasis;
};

} // namespace hal

namespace hal_sandbox {

[NestedUpTo=inside_cpow] sync protocol PHal {
    manager PContent;

child:
    async NotifyBatteryChange(BatteryInformation aBatteryInfo);
    async NotifyUsbStatus(UsbStatus aUsbStatus);
    async NotifyPowerSupplyStatus(PowerSupplyStatus aPowerSupplyStatus);
    async NotifyNetworkChange(NetworkInformation aNetworkInfo);
    async NotifyWakeLockChange(WakeLockInformation aWakeLockInfo);
    async NotifyScreenConfigurationChange(ScreenConfiguration aScreenOrientation);
    async NotifySwitchChange(SwitchEvent aEvent);
    async NotifyFlashlightState(FlashlightInformation aFlashlightInfo);
    async NotifyFlipState(bool aFlipState);

parent:
    async Vibrate(uint32_t[] pattern, uint64_t[] id, PBrowser browser);
    async CancelVibrate(uint64_t[] id, PBrowser browser);

    async EnableFlashlightNotifications();
    async DisableFlashlightNotifications();
    async RequestCurrentFlashlightState();
    async SetFlashlightEnabled(bool enabled);

    async EnableFlipNotifications();
    async DisableFlipNotifications();
    async GetFlipState();

    async EnableBatteryNotifications();
    async DisableBatteryNotifications();
    sync GetCurrentBatteryInformation()
      returns (BatteryInformation aBatteryInfo);
    sync GetBatteryTemperature()
      returns (double temperature);
    sync IsBatteryPresent()
      returns (bool present);

    async EnableUsbNotifications();
    async DisableUsbNotifications();
    sync GetCurrentUsbStatus()
      returns (UsbStatus aUsbStatus);

    async EnablePowerSupplyNotifications();
    async DisablePowerSupplyNotifications();
    sync GetCurrentPowerSupplyStatus()
      returns (PowerSupplyStatus aPowerSupplyStatus);

    async EnableNetworkNotifications();
    async DisableNetworkNotifications();
    sync GetCurrentNetworkInformation()
      returns (NetworkInformation aNetworkInfo);

    sync GetScreenEnabled() returns (bool enabled);
    async SetScreenEnabled(bool aEnabled);

    sync GetExtScreenEnabled() returns (bool enabled);
    async SetExtScreenEnabled(bool aEnabled);

    async ModifyWakeLock(nsString aTopic,
                         WakeLockControl aLockAdjust,
                         WakeLockControl aHiddenAdjust,
                         uint64_t aProcessID);
    async EnableWakeLockNotifications();
    async DisableWakeLockNotifications();
    sync GetWakeLockInfo(nsString aTopic)
      returns (WakeLockInformation aWakeLockInfo);

    async EnableScreenConfigurationNotifications();
    async DisableScreenConfigurationNotifications();
    sync GetCurrentScreenConfiguration()
      returns (ScreenConfiguration aScreenConfiguration);
    async LockScreenOrientation(ScreenOrientation aOrientation)
      returns (bool allowed);
    async UnlockScreenOrientation();
    async EnableSwitchNotifications(SwitchDevice aDevice);
    async DisableSwitchNotifications(SwitchDevice aDevice);
    sync GetCurrentSwitchState(SwitchDevice aDevice)
      returns (SwitchState aState);
child:
    async NotifySensorChange(SensorData aSensorData);

parent:
    async EnableSensorNotifications(SensorType aSensor);
    async DisableSensorNotifications(SensorType aSensor);

    async __delete__();
};

} // namespace hal
} // namespace mozilla
